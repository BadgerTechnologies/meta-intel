#@TYPE: Machine
#@NAME: sys940x

#@WEBTITLE: Intel Atom E6xx Processor with Intel EG20T Controller Hub Development Kit (Queens Bay) with Proprietary IEMGD Accelerated Graphics

#@DESCRIPTION: Machine configuration for Inforce SYS940x systems

require conf/machine/include/tune-atom.inc
require conf/machine/include/ia32-base.inc
require conf/machine/include/meta-intel.inc

# Add "pcbios" if you have a non-efi version of the firmware
MACHINE_FEATURES += "efi pcbios gst-va-mixvideo"

PREFERRED_VERSION_linux-yocto ?= "3.4%"

XSERVER ?= "${XSERVER_IA32_BASE} \
           ${XSERVER_IA32_EXT} \
           ${XSERVER_IA32_EMGD} \
           "

PREFERRED_VERSION_xserver-xorg ?= "1.9.3"
PREFERRED_VERSION_mesa-dri ?= "8.0.4"
PREFERRED_VERSION_xf86-input-evdev ?= "2.6.0"
PREFERRED_VERSION_emgd-driver-bin ?= "1.14"

SERIAL_CONSOLE = "115200 ttyS0"
APPEND += "console=ttyS0,115200 console=tty0"

# Some of the EMGD components have dependency on libx11.
# Add these only when Xserver is enabled with the EMGD driver
# Otherwise unwanted X components will start showing up in the non-X images
VA_FEATURES ?= "gst-va-intel va-intel \
                ${@bb.utils.contains("XSERVER", "emgd-driver-bin", "emgd-driver-video emgd-gst-plugins-va-0.10.10 emgd-gst-plugins-mixvideo-0.10.34", "", d)}"

MACHINE_EXTRA_RRECOMMENDS += "${VA_FEATURES}"
